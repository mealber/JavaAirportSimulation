plugins {
    id 'application'
    id 'jacoco'
    id 'checkstyle'
    id 'com.github.spotbugs' version '5.0.13'
}

repositories {
    mavenCentral()
    gradlePluginPortal()
}

dependencies {
    //JUnit 5
    testImplementation 'org.junit.jupiter:junit-jupiter-api:5.9.0'
    testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine:5.9.0'
    testImplementation 'org.junit.jupiter:junit-jupiter-params:5.9.0'

    //Mockito for mocking in unit tests
    testImplementation 'org.mockito:mockito-core:5.11.0'

    implementation files('cls/')
}

application {
    mainClass = 'Main'
}

test {
    useJUnitPlatform()
    jacoco {
        excludes = ['sun/**', 'jdk/**'] 
    }
}

jacoco {
    toolVersion = "0.8.12"
}

jacocoTestReport {
    dependsOn test
    executionData fileTree(buildDir).include("jacoco/test.exec")
    classDirectories.from = files("$buildDir/classes/java/main")
    sourceDirectories.from = files("src/main/java")
    reports {
        html.required = true
        xml.required = false
        csv.required = false
        html.outputLocation = layout.buildDirectory.dir('jacocoHtml')
    }
}

checkstyle {
    toolVersion = "8.8"
    ignoreFailures = true
    showViolations = false
}

tasks.withType(Checkstyle) {
}

spotbugs {
    toolVersion = '4.8.4'
    ignoreFailures = true
    showProgress = true
    effort = 'max'
    reportLevel = 'low'
}

tasks.withType(com.github.spotbugs.snom.SpotBugsTask).configureEach {
    reports {
        html.required.set(true)
        xml.required.set(false)
    }
}